rootProject.name = "java-aqa-framework"

include "junit-api",
        "junit-ui",
        "cucumber-api",
        "cucumber-ui",
        "core"

dependencyResolutionManagement {
    repositories {
        mavenCentral()
    }

    versionCatalogs {
        libs {
            version("junit-jupiter", "5.8.2")
            version("rest-assured", "5.1.1")

            // Required for all projects
            library("assertj", "org.assertj", "assertj-core").version("3.23.1")
            library("jackson", "com.fasterxml.jackson.core", "jackson-databind").version("2.13.3")

            // API
            library("restassured", "io.rest-assured", "rest-assured").versionRef("rest-assured")
            library("jsonschemavalidator", "io.rest-assured", "json-schema-validator").versionRef("rest-assured")

            // JUnit
            library("junit", "org.junit.jupiter", "junit-jupiter-engine").versionRef("junit-jupiter")
            library("junitparams", "org.junit.jupiter", "junit-jupiter-params").versionRef("junit-jupiter")

            // Bundles
            bundle("junit", ["junit", "junitparams"])
            bundle("restassured", ["restassured", "jsonschemavalidator"])
        }
    }
}
